// SPDX-License-Identifier: UNLICENSED
pragma solidity ^0.8.0;

import "@openzeppelin/contracts/token/ERC20/ERC20.sol";
import "@openzeppelin/contracts/utils/structs/EnumerableSet.sol";

contract WeightedVoting is ERC20 {
    using EnumerableSet for EnumerableSet.AddressSet;

    error TokensClaimed();
    error AllTokensClaimed();
    error NoTokensHeld();
    error QuorumTooHigh(uint quorum);
    error AlreadyVoted();
    error VotingClosed();

    uint private constant MAX_SUPPLY = 1_000_000;
    uint private claimedTokens = 0;

    struct Issue {
        EnumerableSet.AddressSet voters; // Lưu trữ tập hợp địa chỉ
        string issueDesc;
        uint quorum;
        uint totalVotes;
        uint votesFor;
        uint votesAgainst;
        uint votesAbstain;
        bool passed;
        bool closed;
    }

    struct FormattedIssue {
        address[] voters;
        string issueDesc;
        uint quorum;
        uint totalVotes;
        uint votesFor;
        uint votesAgainst;
        uint votesAbstain;
        bool passed;
        bool closed;
    }

    Issue[] private issues;

    enum Vote {
        AGAINST,
        FOR,
        ABSTAIN
    }

    mapping(address => bool) private claimed;

    constructor(
        string memory name_,
        string memory symbol_
    ) ERC20(name_, symbol_) {
        issues.push(); // Thêm một Issue rỗng vào mảng
        Issue storage burntIssue = issues[0]; // Lấy tham chiếu đến Issue vừa thêm
        burntIssue.issueDesc = "burnt";
        burntIssue.quorum = 0;
        burntIssue.totalVotes = 0;
        burntIssue.votesFor = 0;
        burntIssue.votesAgainst = 0;
        burntIssue.votesAbstain = 0;
        burntIssue.passed = false;
        burntIssue.closed = true;
        // Lưu ý: voters được khởi tạo tự động trống, không cần gán
    }

    function claim() external {
        if (claimed[msg.sender]) {
            revert TokensClaimed();
        }
        if (claimedTokens >= MAX_SUPPLY) {
            revert AllTokensClaimed();
        }

        claimed[msg.sender] = true;
        _mint(msg.sender, 100);
        claimedTokens += 100;
    }

    function createIssue(
        string calldata _issueDesc,
        uint _quorum
    ) external returns (uint) {
        if (balanceOf(msg.sender) == 0) {
            revert NoTokensHeld();
        }
        if (_quorum > totalSupply()) {
            revert QuorumTooHigh(_quorum);
        }
        issues.push(); // Thêm một Issue rỗng
        uint issueId = issues.length - 1; // Lấy ID của Issue mới
        Issue storage newIssue = issues[issueId]; // Lấy tham chiếu đến Issue
        newIssue.issueDesc = _issueDesc;
        newIssue.quorum = _quorum;
        newIssue.totalVotes = 0;
        newIssue.votesFor = 0;
        newIssue.votesAgainst = 0;
        newIssue.votesAbstain = 0;
        newIssue.passed = false;
        newIssue.closed = false;
        // voters được khởi tạo tự động trống
        return issueId;
    }

    function getIssue(uint _id) external view returns (FormattedIssue memory) {
        Issue storage issue = issues[_id];
        return
            FormattedIssue({
                voters: issue.voters.values(),
                issueDesc: issue.issueDesc,
                quorum: issue.quorum,
                totalVotes: issue.totalVotes,
                votesFor: issue.votesFor,
                votesAgainst: issue.votesAgainst,
                votesAbstain: issue.votesAbstain,
                passed: issue.passed,
                closed: issue.closed
            });
    }

    function vote(uint _issueId, Vote _vote) external {
        Issue storage issue = issues[_issueId];

        if (issue.closed) {
            revert VotingClosed();
        }
        if (issue.voters.contains(msg.sender)) {
            revert AlreadyVoted();
        }
        uint balance = balanceOf(msg.sender);
        if (balance == 0) {
            revert NoTokensHeld();
        }

        issue.voters.add(msg.sender);

        if (_vote == Vote.AGAINST) {
            issue.votesAgainst += balance;
        } else if (_vote == Vote.FOR) {
            issue.votesFor += balance;
        } else {
            issue.votesAbstain += balance;
        }
        issue.totalVotes += balance;

        if (issue.totalVotes >= issue.quorum) {
            issue.closed = true;
            if (issue.votesFor > issue.votesAgainst) {
                issue.passed = true;
            }
        }
    }
}
